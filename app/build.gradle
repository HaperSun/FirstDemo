apply plugin: 'com.android.application'

android {
    compileSdkVersion build_version.compile_sdk
    buildToolsVersion build_version.build_tools

    defaultConfig {
        multiDexEnabled true
        applicationId config.app.package
        minSdkVersion build_version.min_sdk
        targetSdkVersion build_version.target_sdk
        versionCode config.app.versionCode
        versionName config.app.versionName
        manifestPlaceholders = [
                //友盟
                UMENG_APP_KEY: config.app.umeng_app_key
        ]

        ndk{abiFilters "armeabi"}

        //版本名后面添加一句话，意思就是flavor dimension 它的维度就是该版本号，这样维度就是都是统一的了
        //用来解决All flavors must now belong to a named flavor dimension问题
        flavorDimensions "versionCode"
    }

    signingConfigs {
        debug {
            storeFile file('../key/demo1.jks')
            storePassword '654321'
            keyAlias 'demo1'
            keyPassword '654321'
        }
        release {
            storeFile file('../key/demo1.jks')
            storePassword '654321'
            keyAlias 'demo1'
            keyPassword '654321'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {
        //多渠道打包
        dev {
            applicationId "com.sun.demo1"
            manifestPlaceholders = [
                    APP_NAME : "Harper测试",
                    UMENG_APP_KEY: config.app.umeng_app_key //友盟
            ]
            buildConfigField "String", "Base_URL", config.server.urlTest
            buildConfigField "String", "Base_URL_H5", config.server.urlH5Test
            buildConfigField "boolean", "VAR_BOOLEAN", 'false'
            buildConfigField "String", "VAR_STRING", '"我是String"'
        }
        pro {
            applicationId "com.sun.demo1"
            manifestPlaceholders = [
                    APP_NAME : "Harper",
                    UMENG_APP_KEY: config.app.umeng_app_key //友盟
            ]
            buildConfigField "String", "Base_URL", config.server.urlRelease
            buildConfigField "String", "Base_URL_H5", config.server.urlH5Release
            buildConfigField "boolean", "VAR_BOOLEAN", 'false'
            buildConfigField "String", "VAR_STRING", '"我是String"'
        }
    }

    //生成apk名字的格式：
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def date = new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+08"));
            if (variant.buildType.name.equals('debug')) {
                //可自定义自己想要生成的格式
                outputFileName = "harper_${date}.apk"
            }
            if (variant.buildType.name.equals('release')) {
                outputFileName = "harper_${versionName}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility build_version.version_java
        targetCompatibility build_version.version_java
    }

    //启动dataBinding
    dataBinding {
        enabled = true
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation (target.multitype, { exclude group: 'com.android.support' })
    implementation target.multidex

    //友盟分享
    implementation target.umsdk_common
    implementation target.umsdk_asms
    implementation target.umsdk_link
    implementation target.umsdk_share_core
    implementation target.umsdk_share_board
    implementation target.umsdk_share_wx
    implementation target.wechat_opensdk

    implementation project(':LibBase')
    implementation files('libs\\tbs_sdk_thirdapp_v4.3.0.215_44115_sharewithdownloadwithfile_withoutGame_obfs_20211019_111112.jar')

    //底部tabBar
    implementation('me.majiajie:pager-bottom-tab-strip:2.2.5') {
        exclude group: 'com.android.support'
    }
}